#!/usr/local/bin/b4p
include ( Style Library );      // Include this library if you want to use the 'table style ...' functions.

file download overwrite         ( "https://en.wikipedia.org/wiki/List_of_presidents_of_the_United_States", presidents.html );
table load                      ( presidents, presidents.html, HTML, 'id="Presidents"' );

// Strip all footnote references and new lines in the fields, and the last table row with footnotes inside
table delete rows               ( presidents, table length( presidents ) -1 );
table process all cells         ( presidents, [.] = replace all( literal([.]), { '[?]', new line, '- '}, {'','','-' } ) );

// Remove the blank column originally containing portraits and put president name into all rows


table save( presidents, "as saved.csv" );

table delete columns            ( presidents, {Portrait, Party } );
table rename column headers     ( presidents, {"Presidency (1)", "Party (1)"}, {Period, Party} );
table fill vertically           ( presidents, President );
table consolidate               ( presidents, President, { Election, Vice President }, append, ", " );

table initialize ( party colors,  // Define party colors
    {{ Party Name,              Colors },            { Unaffiliated,   gray 15      },
     { Democratic,              azur   },            { Republican,     imperial red }, 
     { Federalist,              coral  },            { Whig,           yellow       },
     { "Democratic-Republican", excel light green }, { National Union, ocre         } } );


// Add some colors and styles
table process                   ( presidents, table style cells( presidents, Party, row(), single, 
                                      fill color, [ party colors : Party Name, [Party], Colors ] ) );
table style columns             ( presidents, { "Period", "President", "Vice President" }, sheet, column width, 30 );
table style columns             ( presidents, { Party, Election }, sheet, column width, 20, horizontal align, middle );
table style rows                ( presidents, 0, table, boldface, true );
table style table               ( presidents, sheet, wrap text, true, autofilter, 0, freeze rows, 1 );
table save excel file           ( presidents, "All U.S. Pesidents", "Images\Presidents.xlsx" );

// Optionally, save in HTML format, too.

translate style attributes for html (presidents);
table save                      ( presidents, "Images\Presidents.html", HTML );

table list                      ( presidents, briefly, 5, 1st col, 1, last col, 4 ); // List first and last five presidents on the screen.

