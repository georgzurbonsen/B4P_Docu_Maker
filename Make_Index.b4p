
// This B4P program creates an index file

	book[LIB] = "Function Library Guide";
	book[LAN] = "Language Guide";
	book[GUI] = "User Guide";
	book[MAS] = "Master Document";
	book[IDX] = "Index";

	preamble[] = compose ( new line, B4PDOCU.START,		new line, // Begin with line 2
	'"Index" :', 						new line,
	'{',							new line,
	'"Documentation": "General Description",',		new line,
	'"Feature Names": [ "Index" ],' );

	trailer[] = compose( '+++', new line, '}', new line, B4PDOCU.STOP );



// Main Part

{
	table load( toc, "TOC.csv" );

	// Step 1:  Put every keyword into every row

	table process 			( toc, all keywords[] += tokenize( [Keywords], {}, "," ); all features[] += tokenize( [Feature Names], {}, "," ) );

	table process 			( toc, [Keywords] = join( trim(parameter set([Feature Names]) | parameter set([Keywords])), "," );
						[Feature Names] = parameter set([Keywords]){} );
	table rename column headers	( toc, Feature Names, Keyword Count );

	table process			( toc, [Section Nr] = replace all( [Section Nr], '.0','.' ) );

	table insert selected rows	( toc, true /* all */, [Keyword Count]-1, { Section Nr, Section Name, Keywords, HTML File Name }, Keyword Count );
	table process			( toc, [Keywords] = parameter set([Keywords]){[Keyword Count]} );
	table delete columns		( toc, Keyword Count );

	table delete selected rows	( toc, [Section Nr]=='' | [HTML File Name]=='' | [Keywords] = '','(*)' );
	table sort rows			( toc, Keywords, alphabetic ignore case );

	table keep columns		( toc, { Section Nr, HTML File Name, Section Name, Keywords } );

	table initialize		( index, { Index } );
	

	section nr[]			= 1;
	table process 
	( toc,
		if ([Keywords]{0} <> +[Keywords,row()-1]{0})
		{
		    table append	( index, { compose( '"Description ', literal( section nr[]++, "00"), ':', +[Keywords]{0} ,'"::' ) } );
		    keywords[] = {};
		}
		keywords[] += {'__'+[Keywords]+'__'};
		if ([HTML File Name] <> [HTML File Name,row()+1]) // Last row with same reference
		{
		    table append	( index, { join( keywords[], ', ' ) + '<br>', compose( book[[HTML File Name]{0..2}], ': ', right([Section Nr],' '), ' ',[Section Name],'<br><br>'  ) } );
		    
		    keywords[] = {};
		}
	);


	[index:0,0] = preamble[];
	[index:0,table length(index)] = trailer[];

	table save ( index, "Inputs_Index\index.txt", TEXT );

}