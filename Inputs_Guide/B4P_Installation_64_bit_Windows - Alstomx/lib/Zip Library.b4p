

/*
	Zip Library.b4p
	____________________________________________________________________________________________________________


	Provides a set of functions to zip / unzip files with 7z.exe (must be installed separately) !!!

	____________________________________________________________________________________________________________


	Author:			Georg zur Bonsen
				Copyright (C) 2021 by Georg zur Bonsen, All Rights Reserved

	Contents:

	This library contains a set of ZIP compression and extraction functions which can be conveniently used
	in B4P.  Note that the 7Z compression tool must be installed and located in one of the available "C:\Prog..." 
	subdirectories.  Execution will stop if 7Z cannot be found.


	Revision History:
	____________________________________________________________________________________________________________

	Library	B4P		Date		Features

	1.00	5.07				First Release with basic functions
	1.01	5.07		2019-04-03	If ZIP file is locked user can retry
	1.02	6.01		2019-09-10	Resolved some problems with file and directory names containing spaces
	2.00	6.02, 7.00	2020-01-10	Supports both LINUX and WINDOWS
	2.01	6.02, 7.01	2020-02-19	Also OK for MACOS
	2.10			2020-07-24	Misc. updates
	2.11			2020-07-26	Corrections (_zip path)
	2.20	8.00		2020-08-08	Transition to B4P
	2.21	8.00		2020-10-04	Minor improvements
	2.22	8.00		2020-11-08	Simplified welcome text
	2.23	8.00		2020-11-13	Version info moved to system variable
	2.24	8.00		2021-01-03	Minor adaptations for compatibility
	2.25	8.00		2022-01-03	Minor corrections

*/


once
{

	//
	// Version verification
	// _________________________________________________________________________________________________________
	//

	libraries[zip library,version info] = "2.25";
	if (runtime settings[verbose] != quiet) echo("Included: ", "Zip Library", "  ", libraries[zip library,version info] );



	if (numeral( left( version info[], ' ')) < 8.00)
	{
	    echo("Version Mismatch. Prerequisite for using this file 'Zip Library.txt' is B4P Release 8.00 or newer.");
	    echo("Please obtain and install latest available release.");
	    stop;
	}


	//
	// Search the 7Z (Windows) or ZIP (Linux) compression / decompression utility (or retrieve it if already stored)
	// _________________________________________________________________________________________________________
	//


	if (system info[operating system] = Windows)
	{

		libraries[zip library,zip path] = "";
		attribute read( system info[user app data directory b4p], "B4P User Settings.json", "7-Zip Path", libraries[zip library,zip path] );

		if (type(libraries[zip library,zip path]) == parameter set)
		{
			libraries[zip library,zip path] = pick if( libraries[zip library,zip path]{}==0, '', libraries[zip library,zip path]{0} );
		}

		if ((libraries[zip library,zip path]="") | (!file existing(libraries[zip library,zip path]))) // 7-Zip file not found with path stored in user settings
		{

		    echo("Look for the 7Z compression/decomopression utility", new line);

		    program paths[] = search files( "C:\", "Prog*", -1, directories );

		    echo("Found following program file paths: ", program paths[] );

		    for all parameters( program paths[], program path[] )
		    {
			libraries[zip library,zip path] = search files recursive( program path[], "7z.exe" );
			if (libraries[zip library,zip path] <> {}) break loop;
		    }

		    if (libraries[zip library,zip path] = {})
		    {
			exception( stop, "Unable to find 7z.exe.  You need to install this utility", -1 );
		    }

		    attribute write( system info[user app data directory b4p], "B4P User Settings.json", "7-Zip Path", libraries[zip library,zip path] );
		    echo(" - 7z.exe Zip-utility found in ", libraries[zip library,zip path], " (OK)" );

		    delete( program paths[], program path[] );
		}
/*		else
		{
		    if (runtime settings[verbose] != quiet)  echo("  Path to 7z.exe already memorized (OK): ", libraries[zip library,zip path] );
		}
*/	
		libraries[zip library,zip program] = 7z; // Indicates that 7z shall be used.

	}
	else: if (system info[operating system] = Linux,MACOS)
	{
	        global { libraries[zip library,zip path] = ""; libraries[zip library,unzip path] = ""; }
		attribute read( system info[user app data directory b4p], "B4P User Settings.json", "Zip Path", libraries[zip library,zip path] );

		if (type(libraries[zip library,zip path]) == parameter set)
		{
			libraries[zip library,zip path] = pick if( libraries[zip library,zip path]{}==0, '', libraries[zip library,zip path]{0} );
		}

		if ((libraries[zip library,zip path]="") | (!file existing(libraries[zip library,zip path]))) // 7-Zip file not found with path stored in user settings
		{

		    echo(new line, "Look for the Zip compression/decomopression utility", new line);

		    program paths[] = search files( "/bin", "zip", -1, files );
		    if (program paths[] = {}) // Nothing found yet
		    {
			program paths[] = search files( "/usr/bin", "zip", -1, files );
		    }

		    echo("Found following program file paths: ", program paths[] );

		    if (program paths[] <> {} ) // Something found
		    {
			libraries[zip library,zip path] = program paths[]{0};
		    }
		    else
		    {
			exception( stop, "Unable to find zip program.  You need to install this utility", -1 );
		    }

		    attribute write( system info[user app data directory b4p], "B4P User Settings.json", "Zip Path", libraries[zip library,zip path] );
		    echo(new line, "A zip program has been found.  It is located in ", libraries[zip library,zip path] );

		    delete( program paths[] );
		}
		else
		{
		    if (runtime settings[verbose] != quiet) echo("  Path to zip program already memorized (OK): ", libraries[zip library,zip path] );
		}
		libraries[zip library,unzip path] = replace( libraries[zip library,zip path], zip, unzip );
	

	}
	else
	{	
		exception( stop, "Unsupported operating system: " + system info[operating system] + new line + "Supported OS are: Windows, Linux." , -1 );
	}


	//
	// Function library
	// _________________________________________________________________________________________________________
	//

	define procedure( __ zip check file __, {{ archive file name, literal }} )
	{
		if (!file existing( archive file name[]))
		    exception( stop, compose("1st parameter: archive file name = '", archive file name[], "': File not found"), 2 );

		if (!read accessible repeat if locked( archive file name[]))
		    exception( stop, compose("1st parameter: archive file name = '", archive file name[], "': No read access rights"), 2 );

	}



	define procedure ( zip extract files, {{ archive file name, literal }, { content file names, to parameters members to literal}, { target directory name, literal }}, 2, 3 )
	{
		substitute all			( archive file name[], "/", system info[directory separator] );
		substitute all			( archive file name[], "\", system info[directory separator] );
		__ zip check file __		( archive file name[] );		

		final file names[] 		= {};
		quoted content file names[] 	= {};
		for all parameters( content file names[], content file name[] )
		{
		    substitute all		( content file name[], "/", system info[directory separator] );
		    substitute all		( content file name[], "\", system info[directory separator] );

		    final file name[] 		= right last match( right last match( content file name[], "\" ), "/" ); // Remove all path names before.
		    final file names[] 		+= {final file name[]};
		    quoted content file names[]	+= {quote path(content file name[])};
		}

		if (parameter count[]<3)
		{
			target directory name[] = "." + system info[directory separator]; // Current directory if not specified.
		}
		else
		{
			substitute all		( target directory name[], "/", system info[directory separator] );
			substitute all		( target directory name[], "\", system info[directory separator] );

			if (file existing( target directory name[]))
			    exception( stop, compose("Function 'zip extract files':", new line, "3rd parameter: target directory name = '", target directory name[], "': already exists as a file"), 1 );

			if (target directory name[] = "") target directory name[] = "." + system info[directory separator];
			if (target directory name[]{-1} <> system info[directory separator]) target directory name[] += system info[directory separator];
		}

		if (system info[operating system] = Windows)
		{
			command[] 	= quote path(libraries[zip library,zip path]) + ' -y e ' + quote path(archive file name[]) + ' ' + join(quoted content file names[]," ") +
				  ' -o' + quote path(target directory name[]) + ' > NUL';
		}
		else // Linux, MACOS
		{
			command[] 	= libraries[zip library,unzip path]      + ' -qq -o -j ' + quote path(archive file name[])   + ' ' + join(quoted content file names[]," ") +
				  ' -d ' + quote path(target directory name[]);
		}

		rv[] = system(command[]);

		if (rv[] <> 0)
		{
			exception( stop, compose( "Function 'zip extract files':", new line, "unzip command failed: ", command[], new line, "Check all file names."), 1 );
		}

	}


	define procedure ( zip extract all, {{ archive file name, literal }, { target directory name, literal }}, 1, 2 )
	{
		substitute all			( archive file name[], "/", system info[directory separator] );
		substitute all			( archive file name[], "\", system info[directory separator] );
		__ zip check file __		( archive file name[] );		

		if (parameter count[]>=2)
		{
			substitute all		( target directory name[], "/", system info[directory separator] );
			substitute all		( target directory name[], "\", system info[directory separator] );

			if (file existing( target directory name[]))
			    exception( stop, compose("Function 'zip extract all':", new line, "2nd parameter: target directory name = '", target directory name[], "': already exists as a file"), 1 );

			if (target directory name[] = "") target directory name[] = "." + system info[directory separator];
			if (target directory name[]{-1} <> system info[directory separator]) target directory name[] += system info[directory separator];


		}
		else // Target directory name gets same name as archive file name
		{
			target directory name[] = right( left last match( archive file name[], "." ), system info[directory separator] );
		}


		if (system info[operating system] = Windows)
		{
			command[] = quote path(libraries[zip library,zip path]) + ' -y x ' + quote path(archive file name[])
				  + " -o" + quote path(target directory name[]) + ' -r > NUL';
		}
		else // Linux
		{
			command[] 	= libraries[zip library,unzip path]      + ' -qq -o ' + quote path(archive file name[])
				  + ' -d ' + quote path(target directory name[]);
		}


		rv[] = system(command[]);

		if (rv[] <> 0)
		{
			exception( stop, compose( "Function 'zip extract all':", new line, "unzip command failed: ", command[], new line, "Check all file names."), 1 );
		}

		if (!directory existing( target directory name[]))
		    exception( stop, compose("Function 'zip extract files':", new line, "Failed to extract the archive '", target directory name[], "': Possibly problems extracting."), 1 );

	}


	define procedure ( zip compress, {{ source path name, literal }, { archive file name, literal }, { archive type, literal}}, 1, 3 )
	{
		substitute all			( source path name[], "/", system info[directory separator] );
		substitute all			( source path name[], "\", system info[directory separator] );


		if ( ( find(source path name[], "*") == -1) & ( find(source path name[], "?") == -1) ) // No wildcard specified
		{
			if ( (!directory existing( source path name[])) & (!file existing( source path name[])) )
			    exception( stop, compose("Function 'zip compress':", new line, "1st parameter: Source Path Name = '", source path name[], "': Neither existing file or directory found"), 1 );
		}
		else
		{
			if (parameter count[]==1)
			    exception( stop, compose("Function 'zip compress':", new line, "1st paramter: Source Path Name = '", source path name[], "' contains wildcard symbols (* and/or %).", new line,
						"2nd parameter (archive file name) is required." ), 1);
		}


		if (parameter count[]>=2)
		{
		    substitute all		( archive file name[], "/", system info[directory separator] );
		    substitute all		( archive file name[], "\", system info[directory separator] );

		    if (directory existing( archive file name[]))
			exception( stop, compose("Function 'zip compress':", new line, "2nd parameter: archive file name = '", archive file name[], "': Exists as directory"), 1 );
		}
		else
		{
		    archive file name[] = right( left last match( source path name[], "." ), system info[directory separator] );
		}


		if (parameter count[]>=3)
		{
		    allowed archive types[] = select if ( system info[operating system] == Windows, 'zip,7z', zip ); // LINUX: zip only
		    if (archive type[] <> allowed archive types[])
			exception( stop, compose("Function 'zip compress':", new line, "3rd parameter: archive type = '", archive file name[], "': Allowed: " + allowed archive types[]), 1 );
		}
		else
		{
		    archive type[] = "zip";
		}


		if (archive file name[] <> '*.' + softquoted literal( archive type[] ) ) // Add .zip or .7s after archive file name if not specified
		{
		    archive file name[] += '.' + archive type[];   
		}


		if (system info[operating system] = Windows)
		{
		    command[] = quote path(libraries[zip library,zip path]) + ' -y a -t' + archive type[] + ' ' + quote path(archive file name[]) + ' ' 
		              + quote path(source path name[]) + ' -r > NUL';

		}
		else // Linux
		{
		    command[] = libraries[zip library,zip path] + ' -r -q ' + quote path(archive file name[]) + ' ' + quote path(source path name[]);
		}

		rv[] = system(command[]);
		if (rv[] <> 0)
		{
			exception( stop, compose("Function 'zip compress':", new line, "zip command failed: ", command[], new line, "Check all file names."), 1 );
		}

		if (!file existing( archive file name[]))
		    exception( stop, compose("Failed to create the archive '", archive file name[]), 1 );

	}

}

